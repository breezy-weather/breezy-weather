plugins {
    id 'com.android.application'
    id 'org.greenrobot.greendao'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdkVersion 32
    defaultConfig {
        applicationId "wangdaye.com.geometricweather"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 30017
        versionName "3.017"
        multiDexEnabled true
        ndk {
            abiFilters 'armeabi', 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }
    ndkVersion "22.0.6917172"
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "GeometricWeather ${variant.versionName}.apk"
                }
            }
        }
    }
    buildTypes.each {
        it.buildConfigField 'String', 'CN_WEATHER_BASE_URL', CN_WEATHER_BASE_URL
        it.buildConfigField 'String', 'CAIYUN_WEATHER_BASE_URL', CAIYUN_WEATHER_BASE_URL
        it.buildConfigField 'String', 'ACCU_WEATHER_BASE_URL', ACCU_WEATHER_BASE_URL
        it.buildConfigField 'String', 'ACCU_WEATHER_KEY', ACCU_WEATHER_KEY
        it.buildConfigField 'String', 'ACCU_CURRENT_KEY', ACCU_CURRENT_KEY
        it.buildConfigField 'String', 'ACCU_AQI_KEY', ACCU_AQI_KEY
        it.buildConfigField 'String', 'OWM_KEY', OWM_KEY
        it.buildConfigField 'String', 'OWM_BASE_URL', OWM_BASE_URL
        it.buildConfigField 'String', 'BAIDU_IP_LOCATION_BASE_URL', BAIDU_IP_LOCATION_BASE_URL
        it.buildConfigField 'String', 'BAIDU_IP_LOCATION_AK', BAIDU_IP_LOCATION_AK
        it.buildConfigField 'String', 'MF_WSFT_KEY', MF_WSFT_KEY
        it.buildConfigField 'String', 'MF_WSFT_BASE_URL', MF_WSFT_BASE_URL
        it.buildConfigField 'String', 'IQA_AIR_PARIF_KEY', IQA_AIR_PARIF_KEY
        it.buildConfigField 'String', 'IQA_AIR_PARIF_URL', IQA_AIR_PARIF_URL
        it.buildConfigField 'String', 'IQA_ATMO_AURA_KEY', IQA_ATMO_AURA_KEY
        it.buildConfigField 'String', 'IQA_ATMO_AURA_URL', IQA_ATMO_AURA_URL
        it.buildConfigField 'String', 'IQA_ATMO_SUD_URL', IQA_ATMO_SUD_URL
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    sourceSets{
        pub {
            java.srcDirs = ["src/bugly", "src/amap", "src/proprietary"]
            manifest.srcFile "proprietary/AndroidManifest.xml"
            jniLibs.srcDir 'libs'
            jni.srcDirs = []
        }
        gplay {
            java.srcDirs = ["src/nobugly", "src/noamap", "src/proprietary"]
            manifest.srcFile "proprietary/AndroidManifest.xml"
            jniLibs.srcDir 'libs'
            jni.srcDirs = []
        }
        fdroid {
            java.srcDirs = ["src/nobugly", "src/noamap", "src/opensource"]
        }
        test {
            java.srcDir 'test'
        }
        androidTest {
            java.srcDir 'androidTest'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            unitTests.returnDefaultValues = true
        }
    }
    flavorDimensions "store"
    productFlavors {
        pub {
            dimension "store"
            versionNameSuffix "_pub"
        }
        gplay {
            dimension "store"
            versionNameSuffix "_gplay"
            repositories {
                maven {
                    url "https://dl.bintray.com/qichuan/maven/"
                }
            }
        }
        fdroid {
            dimension "store"
            versionNameSuffix "_fdroid"
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven {
            url "https://jitpack.io"
        }
    }
}

configurations {
    cleanedAnnotations
    compile.exclude group: 'org.jetbrains' , module:'annotations'
}

greendao {
    schemaVersion 60
    generateTests false
}

dependencies {
    pubImplementation fileTree(include: ['*.jar'], dir: 'libs')
    gplayImplementation fileTree(include: ['*.jar'], dir: 'libs')

    // kotlin.
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // test.
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'androidx.test:runner:1.4.0'
    testImplementation 'androidx.test:rules:1.4.0'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.9'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.9'
    testImplementation 'org.powermock:powermock-module-junit4-rule:2.0.9'
    testImplementation "org.powermock:powermock-classloading-xstream:2.0.9"
    testImplementation 'org.robolectric:robolectric:4.3'

    // material design.
    implementation 'com.google.android.material:material:1.6.0-alpha02'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // preference.
    implementation 'androidx.preference:preference-ktx:1.1.1'

    // work.
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.work:work-runtime:2.7.0-beta01"

    // lifecycle.
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"
    //noinspection LifecycleAnnotationProcessorWithJava8
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:2.3.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:2.3.1"

    // multi dex.
    implementation "androidx.multidex:multidex:2.0.1"

    // hilt.
    implementation "com.google.dagger:hilt-android:2.28-alpha"
    kapt "com.google.dagger:hilt-android-compiler:2.28-alpha"

    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha01'
    implementation 'androidx.hilt:hilt-work:1.0.0-alpha01'

    kapt 'androidx.hilt:hilt-compiler:1.0.0-alpha01'
    annotationProcessor 'androidx.hilt:hilt-compiler:1.0.0-alpha01'

    // a map.
    pubImplementation 'com.amap.api:location:5.3.1'

    // gms.
    pubImplementation "com.google.android.gms:play-services-location:19.0.1"
    gplayImplementation "com.google.android.gms:play-services-location:19.0.1"

    // glide.
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.11.0'

    // retrofit.
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.2'
    implementation('com.squareup.okhttp3:okhttp') {
        version {strictly '3.12.12'}
    }
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.12'

    // db.
    implementation 'org.greenrobot:greendao:3.3.0'
    implementation 'io.github.yuweiguocn:GreenDaoUpgradeHelper:v2.2.1'

    // data store
    // implementation "androidx.datastore:datastore-preferences:1.0.0-alpha07"
    // implementation "androidx.datastore:datastore-preferences-rxjava2:1.0.0-alpha07"

    // gson.
    implementation 'com.google.code.gson:gson:2.8.6'

    // cm.
    implementation 'org.cyanogenmod:platform.sdk:5.0'

    // bugly.
    pubImplementation 'com.tencent.bugly:crashreport:2.8.6.0'
    pubImplementation 'com.tencent.bugly:nativecrashreport:3.6.0.1'

    // rx java.
    implementation "io.reactivex.rxjava2:rxjava:2.2.9"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.github.nekocode.rxlifecycle:rxlifecycle:2.0'
    implementation 'com.github.nekocode.rxlifecycle:rxlifecycle-compact:2.0'

    // ui.
    implementation 'com.github.rahatarmanahmed:circularprogressview:2.5.0'
    implementation 'com.jaredrummler:colorpicker:1.1.0'
    implementation 'com.xw.repo:bubbleseekbar:3.20-lite'
    implementation 'me.jfenn:AdaptiveIconView:0.0.1'
    implementation 'com.github.turing-tech:MaterialScrollBar:13.3.2'
    implementation 'com.gordonwong:material-sheet-fab:1.2.1'

    // utils.
    implementation 'com.github.XhinLiang:LunarCalendar:3.0.0'
    implementation 'com.github.didikee:AndroidDonate:0.1.0'
}